#version 400 core

//variables are in because they come from VAO data
in vec3 position;
in vec2 textureCoords;
in vec3 normal;

out vec2 pass_textureCoords;
out vec3 surfaceNormal;
out vec3 toLightVector[4];
out vec3 toCameraVector;
out float visibility;

//use this in vertex shader to update location, rotation, and scale because faster
uniform mat4 transformationMatrix;
uniform mat4 projectionMatrix;
uniform mat4 viewMatrix;
uniform vec3 lightPosition[4];

const float density = 0.002; //general visibility of entity
const float gradient = 7; //transition from normal to fog

uniform vec4 plane;

void main(void) {
	vec4 worldPosition = transformationMatrix * vec4(position, 1.0);
		
	gl_ClipDistance[0] = dot(worldPosition, plane);
		
	vec4 positionRelativeToCam = viewMatrix * worldPosition; //give vertex position in relation to camera

	gl_Position = projectionMatrix * positionRelativeToCam;
	
	//tiling done in fragment shader
	pass_textureCoords = textureCoords;
	
	//look up the dot product for vectors
	surfaceNormal = (transformationMatrix * vec4(normal, 0.0)).xyz;
	
	//calculate toLightVector 4 times
	for(int i = 0; i < 4; i++){
		toLightVector[i] = lightPosition[i] - worldPosition.xyz;
	}
	
	toCameraVector = (inverse(viewMatrix) * vec4(0.0, 0.0, 0.0, 1.0)).xyz - worldPosition.xyz;
	
	float distance = length(positionRelativeToCam.xyz);
	visibility = exp(-pow((distance * density), gradient));
	visibility = clamp(visibility, 0.0, 1.0); //make sure visibility stays in 0-1
}