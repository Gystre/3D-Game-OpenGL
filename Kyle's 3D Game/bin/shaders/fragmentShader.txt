#version 400 core

in vec2 pass_textureCoords;
in vec3 surfaceNormal;
in vec3 toLightVector[4];
in vec3 toCameraVector;
in float visibility;

out vec4 out_Color; //output color of pixel

//uniform variables can be changed
uniform sampler2D textureSampler;
uniform vec3 lightColor[4];
uniform vec3 attenuation[4];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColor; //color the object will fade out to

void main(void) {
	//normalizing vector makes size equal to 1
	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitVectorToCamera = normalize(toCameraVector);
	
	vec3 totalDiffuse = vec3(0.0);
	vec3 totalSpecular = vec3(0.0);
	
	//lighting stuff
	//needs to be run 4 times because there can be 4 lights that will affect player
	//a way to optimize this is to update the lights when they receive new information so we don't have to update them all the time
	for(int i = 0; i < 4; i++){
		//attenuation calculations
		float distance = length(toLightVector[i]);
		float attFactor = attenuation[i].x + (attenuation[i].y * distance) + (attenuation[i].z * distance * distance); //look up attenuation equation
		
		vec3 unitLightVector = normalize(toLightVector[i]);
		
		//look up dot product for vectors
		float nDot1 = dot(unitNormal, unitLightVector);
		
		//calculate ambient lighting
		float brightness = max(nDot1, 0.0);
		
		vec3 lightDirection = -unitLightVector;
		vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);
		
		float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);
		specularFactor = max(specularFactor, 0.0);
		
		float dampedFactor = pow(specularFactor, shineDamper);
		
		//calculate diffuse and specular lighting coming from each light source
		//keep a running total of all diffuse and specular lighting
		totalDiffuse = totalDiffuse + (brightness * lightColor[i]) / attFactor;
		totalSpecular = totalSpecular + (dampedFactor * reflectivity * lightColor[i]) / attFactor;
	}
	
	//calculate total ambient lighting and make sure it doesn't go below 0.2
	totalDiffuse = max(totalDiffuse, 0.2);
	
	//if alpha is less than 0.5 discard pixel
	vec4 textureColor = texture(textureSampler, pass_textureCoords);
	if(textureColor.a < 0.5){
		discard;
	}

	//returns the color of the pixel at the textures coords
	out_Color = vec4(totalDiffuse, 1.0) * textureColor + vec4(totalSpecular, 1.0);
	out_Color = mix(vec4(skyColor, 1.0), out_Color, visibility);
}